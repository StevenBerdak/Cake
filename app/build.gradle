apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.sbcode.cake"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
    }

    signingConfigs {
        debug {
            storeFile file(keystoreProperties['debugStoreFile'])
            keyAlias keystoreProperties['debugKeyAlias']
            keyPassword keystoreProperties['debugKeyPassword']
            storePassword keystoreProperties['debugStorePassword']
        }
        release {
            storeFile file(keystoreProperties['releaseStoreFile'])
            keyAlias keystoreProperties['releaseKeyAlias']
            keyPassword keystoreProperties['releaseKeyPassword']
            storePassword keystoreProperties['releaseStorePassword']
        }
    }

    buildTypes {
        debugMockData {
            signingConfig signingConfigs.debug
            debuggable true
            applicationIdSuffix ".debugMockData"
        }
        debug {
            signingConfig signingConfigs.debug
            debuggable true
            applicationIdSuffix ".debug"
        }
        release {
            signingConfig signingConfigs.release
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildTypes.each {
        it.buildConfigField("String", "TWITTER_CONSUMER_KEY", twitter_consumer_api_key)
        it.buildConfigField("String", "TWITTER_CONSUMER_SECRET", twitter_consumer_api_secret_key)
    }

    buildTypes.debug {
        it.buildConfigField("String", "TEST_DEVICE_ID", test_device_id)
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildToolsVersion '28.0.3'

    packagingOptions {
        exclude 'META-INF/proguard/androidx-annotations.pro'
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    def support_version = "28.0.0"
    def butterknife_version = "8.8.1"
    def room_version = "1.1.1"
    def paging_version = "1.0.1"
    def facebook_api_version = "4.35.0"
    def twitter_api_version = "3.3.0"
    def retrofit_version = "2.4.0"
    def lifecycle_version = "1.1.1"
    def dagger_version = "2.17"
    def timber_version = "4.7.1"
    def stetho_version = "1.5.0"
    def glide_version = "4.8.0"
    def robolectric_version = "4.0"

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //Android libs
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"
    implementation "android.arch.paging:runtime:$paging_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation "com.android.support:customtabs:$support_version"
    implementation "com.android.support:support-v4:$support_version"
    implementation 'com.android.support:multidex:1.0.3'

    //Google Play Services
    implementation "com.google.android.gms:play-services-analytics:16.0.4"
    implementation 'com.google.android.gms:play-services-ads:17.0.0'

    // ViewModel and LiveData
    implementation "android.arch.lifecycle:extensions:$lifecycle_version"
    implementation "android.arch.lifecycle:runtime:$lifecycle_version"
    implementation "android.arch.lifecycle:common-java8:$lifecycle_version"
    testImplementation "android.arch.core:core-testing:$lifecycle_version"

    //Room
    implementation "android.arch.persistence.room:runtime:$room_version"
    annotationProcessor "android.arch.persistence.room:compiler:$room_version"
    testImplementation "android.arch.persistence.room:testing:$room_version"

    //Retrofit + Moshi
    //implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"

    //Butterknife
    implementation "com.jakewharton:butterknife:$butterknife_version"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknife_version"

    //Timber
    implementation "com.jakewharton.timber:timber:$timber_version"

    //Dagger2
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-android-processor:$dagger_version"

    //Glide
    implementation "com.github.bumptech.glide:glide:$glide_version"
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"

    //Stetho
    implementation "com.facebook.stetho:stetho:$stetho_version"

    //Social Media libs
    implementation("com.facebook.android:facebook-login:$facebook_api_version") {
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'cardview-v7'
        exclude group: 'com.android.support', module: 'customtabs'
    }
    implementation "com.twitter.sdk.android:twitter:$twitter_api_version"

    //Android Testing libs
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    //Robolectric
    testImplementation "org.robolectric:robolectric:$robolectric_version"
    testImplementation "org.robolectric:shadows-multidex:$robolectric_version"
}
